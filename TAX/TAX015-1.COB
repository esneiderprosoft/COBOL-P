004820*=================================================================
004830* CONSULTA INFORMACION DE VEHICULO (CARRO) - INTERMUNICIPAL
004840*=================================================================
004850 IDENTIFICATION DIVISION.
004860 PROGRAM-ID. "HttpExtensionProc". 
004870 ENVIRONMENT DIVISION.
004880 CONFIGURATION SECTION.
004890 INPUT-OUTPUT SECTION.
004900 FILE-CONTROL.
004910     COPY "P:\PROG\PROYECT\FUENTES\FS-USUNET.CBL".
004920     COPY "P:\PROG\PROYECT\FUENTES\FS-SESION.CBL".
004930     COPY "P:\PROG\PROYECT\FUENTES\FS-CARRO-TAX.CBL".

005080 DATA DIVISION.
005090 FILE SECTION.
005100 COPY "P:\PROG\PROYECT\FUENTES\FD-USUNET.CBL".
005110 COPY "P:\PROG\PROYECT\FUENTES\FD-SESION.CBL".
000690 COPY "P:\PROG\PROYECT\FUENTES\FD-CARRO-TAX.CBL".

005190 WORKING-STORAGE SECTION.
005200 COPY "P:\PROG\PROYECT\FUENTES\COBW3.CBL".
005210 COPY "P:\PROG\PROYECT\FUENTES\WEB-CARAC19.CBL".
005220 

       77 DATOS-PLANO-W             PIC X(500).
       77 NOM-ART-W                 PIC X(70).
       77 NOM-MAE-W                 PIC X(70).

       01 FONDO-EDIT                PIC 9.9.
       01 FONDO-R-EDIT              PIC 9.9.
       01 PORCENT-EMPRE-EDIT        PIC 99.99.

         
       01  REG-CAR-W.
           02 CODIGO-CAR-W          PIC X(6).
           02 FILLER                PIC X VALUE "|".
           02 PROPIET-CAR-W         PIC X(10).
           02 FILLER                PIC X VALUE "|".
           02 MARCA-CAR-W           PIC X(20).
           02 FILLER                PIC X VALUE "|".
           02 MOTOR-CAR-W           PIC X(20).
           02 FILLER                PIC X VALUE "|".
           02 CHASIS-CAR-W          PIC X(20).
           02 FILLER                PIC X VALUE "|".
           02 MODALIDAD-CAR-W       PIC X.
           02 FILLER                PIC X VALUE "|".
           02 MODELO-CAR-W          PIC X(4).
           02 FILLER                PIC X VALUE "|".
           02 PASAJEROS-CAR-W       PIC XX.
           02 FILLER                PIC X VALUE "|".
           02 PORC-EMPRESA-CAR-W    PIC X(5).
           02 FILLER                PIC X VALUE "|".
           02 FONDO-CAR-W           PIC X(3).
           02 FILLER                PIC X VALUE "|".
           02 FONDO-R-CAR-W         PIC X(3).
           02 FILLER                PIC X VALUE "|".
           02 COMBUSTIBLE-CAR-W     PIC X.
           02 FILLER                PIC X VALUE "|".
           02 NATURALEZA-CAR-W      PIC X.
           02 FILLER                PIC X VALUE "|".
           02 INTERNO-CAR-W         PIC X(4).
           02 FILLER                PIC X VALUE "|".
           02 T-OPERAC-CAR-W        PIC X(8).
           02 FILLER                PIC X VALUE "|".
           02 FECHA-AFI-CAR-W       PIC X(8).
           02 FILLER                PIC X VALUE "|".
           02 FECHA-RET-CAR-W       PIC X(8).
           02 FILLER                PIC X VALUE "|".
           02 C-COSTO-CAR-W         PIC X(4).
           02 FILLER                PIC X VALUE "|".


006310 LINKAGE SECTION.
006320 COPY "P:\PROG\PROYECT\FUENTES\ISAPICTX.CBL".
006330 PROCEDURE DIVISION WITH stdcall LINKAGE USING ISAPI-CTX-CNT.
006340
006350 DECLARATIVES.
006360 I-O-TEST SECTION.
006370     USE AFTER EXCEPTION PROCEDURE ON ARCHIVO-USUNET.         
006380 ESCR-EXCEPTIONES.
006390     IF OTR-STAT = "00"
006400        CONTINUE 
006410     ELSE
006420        MOVE OTR-STAT         TO MSJ1-HTML
006430        MOVE NOM-USU-W        TO MSJ2-HTML
006440        MOVE "TAX015-1"       TO MSJ3-HTML
006450        GO TO ENVIAR2-ERROR
006460     END-IF.
006470
006480 I-O-TEST SECTION.
006490     USE AFTER EXCEPTION PROCEDURE ON ARCHIVO-SESION.
006500 ESCR-EXCEPTIONES.
006510     IF OTR-STAT = "00" 
006520        CONTINUE 
006530     ELSE
006540        MOVE OTR-STAT         TO MSJ1-HTML
006550        MOVE NOM-SESION-W     TO MSJ2-HTML
006560        MOVE "TAX015-1"       TO MSJ3-HTML
006570        GO TO ENVIAR2-ERROR
006580     END-IF.
006590  
002694 I-O-TEST SECTION.
002700     USE AFTER EXCEPTION PROCEDURE ON ARCHIVO-CARROS.
002710 ESCR-EXCEPTIONES.
002720     IF OTR-STAT = "00"
002730        CONTINUE 
002740     ELSE
002750        MOVE OTR-STAT         TO MSJ1-HTML
002760        MOVE NOM-CAR-TAX-LNK  TO MSJ2-HTML
002770        MOVE "TAX015-1"       TO MSJ3-HTML
002780        GO TO ENVIAR2-ERROR
002790     END-IF.

006720  END DECLARATIVES.
006730
006740 INICIAR-IIS.
006750     MOVE LOW-VALUE TO COBW3.
006760     MOVE FUNCTION ADDR(ISAPI-CTX-CNT) TO COBW3-CONTEXT.
006770     CALL "COBW3_INIT" USING COBW3.
006780
006790 LEER-DATO-HTML.
006800     MOVE "datosh" TO COBW3-SEARCH-DATA.
006810     CALL "COBW3_GET_VALUE" USING COBW3.
006820     MOVE COBW3-GET-DATA    TO LINEA-LLEGAD-W.

           INITIALIZE DATO-LLEGADA-W

           UNSTRING LINEA-LLEGAD-W DELIMITED BY "|"
              INTO SESION-LLEGAD-W, DIRECTORIO-LLEGAD-W, CARPTA-LLEGAD-W, CODIGO-CAR-W
           END-UNSTRING.

           MOVE SESION-LLEGAD-W    TO LLAVE-SESION-W.
           MOVE FUNCTION CURRENT-DATE TO FECHA-TOTAL.
           ACCEPT HORA-TOTAL FROM TIME.

000797 INICIAR-SESION.
000806     MOVE "D:\WEB\MAIN-ELECT\DATOS\SC-SESION.DAT" TO NOM-SESION-W.
000810     GO TO VALIDAR-SESION.
000850
000860 FIN-VALIDAR-SESION.
006840     
006900 ABRIR-USUARIO.
006910     INITIALIZE OTR-STAT.
006920     MOVE "D:\WEB\MAIN-ELECT\DATOS\SC-ARCHUSU.DAT" TO NOM-USU-W
006930
006940     OPEN INPUT ARCHIVO-USUNET
006950     EVALUATE OTR-STAT
006960       WHEN "00"  CONTINUE
006970       WHEN "35"  OPEN OUTPUT ARCHIVO-USUNET
006980                  INITIALIZE REG-USUNET
006990                  WRITE REG-USUNET
007000                  CLOSE ARCHIVO-USUNET
007010                  MOVE "SC-2"                        TO MSJ1-HTML
007020                  MOVE "Falta configurar usuarios"   TO MSJ2-HTML
007030                  MOVE "TAX015-1"                    TO MSJ3-HTML
007040                  GO TO ENVIAR2-ERROR
007050       WHEN OTHER GO TO CERRAR-SESION
007060     END-EVALUATE.
007070
007080     INITIALIZE LLAVE-USUNET.

007110 LEER-USUARIO.
007120     READ ARCHIVO-USUNET NEXT AT END MOVE 0 TO SW-FIN.
007130
007140     CLOSE ARCHIVO-USUNET.
007200     
007210     IF NOMBRE-USUNET = SPACES
007220     OR NIT-USUNET = ZEROS
007230        MOVE "SC-2"      TO MSJ1-HTML
007240        MOVE "Falta configurar usuarios" TO MSJ2-HTML
007250        MOVE "TAX015-1"  TO MSJ3-HTML
007260        GO TO ENVIAR2-ERROR
007270     END-IF.
007280 
007350
       ASIGNAR-NOMBRES.

           INITIALIZE NOM-CAR-TAX-LNK

           INSPECT IP-DATOS-USUNET REPLACING FIRST "/" BY "\"
007287     MOVE "\\" TO NOM-CAR-TAX-LNK
007288     INSPECT NOM-CAR-TAX-LNK REPLACING FIRST "                    "
007289                                    BY IP-DATOS-USUNET
007290     INSPECT NOM-CAR-TAX-LNK REPLACING FIRST " "
007291                                    BY "\"
007292     INSPECT NOM-CAR-TAX-LNK REPLACING FIRST "                              "
007293                                    BY DIRECTORIO-LLEGAD-W

           INSPECT NOM-CAR-TAX-LNK REPLACING FIRST "                        "
                                                BY "\CONTROL\SC-ARCHCARR.DAT".

       ABRIR-CARROS.
           OPEN INPUT ARCHIVO-CARROS.

           MOVE CODIGO-CAR-W  TO CODIGO-CAR
           READ ARCHIVO-CARROS WITH NO LOCK
                INVALID KEY 
                        MOVE "SC-2"     TO MSJ1-HTML
                        MOVE "01"       TO MSJ2-HTML
                        MOVE "TAX015-1" TO MSJ3-HTML
                        GO TO ENVIAR2-ERROR
           END-READ

           CLOSE ARCHIVO-CARROS


           MOVE PROPIET-CAR         TO PROPIET-CAR-W
           MOVE INTERNO-CAR         TO INTERNO-CAR-W
           MOVE MODELO-CAR          TO MODELO-CAR-W
           MOVE PASAJEROS-CAR       TO PASAJEROS-CAR-W
           MOVE COMBUSTIBLE-CAR     TO COMBUSTIBLE-CAR-W
           MOVE NATURALEZA-CAR      TO NATURALEZA-CAR-W
           MOVE MARCA-CAR           TO MARCA-CAR-W
           MOVE MOTOR-CAR           TO MOTOR-CAR-W
           MOVE CHASIS-CAR          TO CHASIS-CAR-W
           MOVE T-OPERAC-CAR        TO T-OPERAC-CAR-W

           MOVE FONDO-CAR           TO FONDO-EDIT 
           MOVE FONDO-EDIT          TO FONDO-CAR-W

           MOVE FONDO-R-CAR         TO FONDO-R-EDIT 
           MOVE FONDO-R-EDIT        TO FONDO-R-CAR-W

           MOVE MODALIDAD-CAR       TO MODALIDAD-CAR-W
           MOVE C-COSTO-CAR         TO C-COSTO-CAR-W
           MOVE FECHA-AFI-CAR       TO FECHA-AFI-CAR-W
           MOVE FECHA-RET-CAR       TO FECHA-RET-CAR-W

           MOVE PORC-EMPRESA-CAR    TO PORCENT-EMPRE-EDIT  
           MOVE PORCENT-EMPRE-EDIT  TO PORC-EMPRESA-CAR-W



           INSPECT REG-CAR-W REPLACING ALL LOW-VALUES BY SPACES.

010700 PAGINA-CONFIG.  
010720     MOVE "statuscode" TO COBW3-CNV-NAME
010730     MOVE "00"         TO COBW3-CNV-VALUE
010740     CALL "COBW3_SET_CNV" USING COBW3

000570	   MOVE "datosrecibidos" TO COBW3-CNV-NAME
000580     MOVE REG-CAR-W        TO COBW3-CNV-VALUE
000590	   CALL "COBW3_SET_CNV"  USING COBW3

010720     MOVE "programa-id" TO COBW3-CNV-NAME
010730     MOVE "TAX015-1"    TO COBW3-CNV-VALUE
010740     CALL "COBW3_SET_CNV" USING COBW3

010760     MOVE "..\PAGINAS\RECIBIDOSPLANO.ASPX" TO SALIDA-HTML
010770     PERFORM ABRIR-HTML.

007990 CERRAR-SESION.
008000     CALL "COBW3_FREE" USING COBW3.
008010     MOVE 1 TO PROGRAM-STATUS.
008020     EXIT PROGRAM.
008040 COPY "P:\PROG\PROYECT\FUENTES\SC-WEB19.CBL".
