000002 IDENTIFICATION DIVISION.
000003 PROGRAM-ID. FAC146.
000004*         PROGRAMA PARA MONTO ESCRITO - MILES DE MILLONES.
000005 ENVIRONMENT DIVISION.
000006 CONFIGURATION SECTION.
000007 SOURCE-COMPUTER. PROSOFT.
000008 OBJECT-COMPUTER. PROSOFT.
000009 DATA DIVISION.
000010 WORKING-STORAGE SECTION.
000011 77  CENT-EDIT           PIC 99V99.
000012 01  VARIABLES.
000013     05 CIEN             PIC X(19)  VALUE "CIEN-              ".
000014     05 J                PIC 9(3)   VALUE ZEROS.
000015     05 K                PIC 9(3)   VALUE ZEROS.
000016     05 Z                PIC 9(3)   VALUE ZEROS.
000017     05 L                PIC 99     VALUE ZEROS.
000018 01  TABLA-CIENTOS.
000019     03 TAB-CIENTOS.
000020        05 X1            PIC X(19)  VALUE "CIENTO-            ".
000021        05 X2            PIC X(19)  VALUE "DOSCIENTOS-        ".
000022        05 X3            PIC X(19)  VALUE "TRESCIENTOS-       ".
000023        05 X4            PIC X(19)  VALUE "CUATROCIENTOS-     ".
000024        05 X5            PIC X(19)  VALUE "QUINIENTOS-        ".
000025        05 X6            PIC X(19)  VALUE "SEISCIENTOS-       ".
000026        05 X7            PIC X(19)  VALUE "SETECIENTOS-       ".
000027        05 X8            PIC X(19)  VALUE "OCHOCIENTOS-       ".
000028        05 X9            PIC X(19)  VALUE "NOVECIENTOS-       ".
000029     03 T-CIENTOS REDEFINES TAB-CIENTOS PIC X(19) OCCURS 9.
000030
000031 01  TABLA-UNIDADES.
000032     03 TAB-UNIDADES.
000033       05 Z1            PIC X(19)  VALUE "UN-                ".
000034       05 Z2            PIC X(19)  VALUE "DOS-               ".
000035       05 Z3            PIC X(19)  VALUE "TRES-              ".
000036       05 Z4            PIC X(19)  VALUE "CUATRO-            ".
000037       05 Z5            PIC X(19)  VALUE "CINCO-             ".
000038       05 Z6            PIC X(19)  VALUE "SEIS-              ".
000039       05 Z7            PIC X(19)  VALUE "SIETE-             ".
000040       05 Z8            PIC X(19)  VALUE "OCHO-              ".
000041       05 Z9            PIC X(19)  VALUE "NUEVE-             ".
000042       05 Z10           PIC X(19)  VALUE "DIEZ-              ".
000043       05 Z11           PIC X(19)  VALUE "ONCE-              ".
000044       05 Z12           PIC X(19)  VALUE "DOCE-              ".
000045       05 Z13           PIC X(19)  VALUE "TRECE-             ".
000046       05 Z14           PIC X(19)  VALUE "CATORCE-           ".
000047       05 Z15           PIC X(19)  VALUE "QUINCE-            ".
000048       05 Z16           PIC X(19)  VALUE "DIECISEIS-         ".
000049       05 Z17           PIC X(19)  VALUE "DIECISIETE-        ".
000050       05 Z18           PIC X(19)  VALUE "DIECIOCHO-         ".
000051       05 Z19           PIC X(19)  VALUE "DIECINUEVE-        ".
000052       05 Z20           PIC X(19)  VALUE "VEINTE-            ".
000053       05 Z21           PIC X(19)  VALUE "VENTIUN-           ".
000054       05 Z22           PIC X(19)  VALUE "VENTIDOS-          ".
000055       05 Z23           PIC X(19)  VALUE "VENTITRES-         ".
000056       05 Z24           PIC X(19)  VALUE "VENTICUATRO-       ".
000057       05 Z25           PIC X(19)  VALUE "VENTICINCO-        ".
000058       05 Z26           PIC X(19)  VALUE "VENTISEIS-         ".
000059       05 Z27           PIC X(19)  VALUE "VENTISIETE-        ".
000060       05 Z28           PIC X(19)  VALUE "VENTIOCHO-         ".
000061       05 Z29           PIC X(19)  VALUE "VENTINUEVE-        ".
000062       05 Z30           PIC X(19)  VALUE "TREINTA-           ".
000063       05 Z31           PIC X(19)  VALUE "TREINTA-Y-UN-      ".
000064       05 Z32           PIC X(19)  VALUE "TREINTA-Y-DOS-     ".
000065       05 Z33           PIC X(19)  VALUE "TREINTA-Y-TRES-    ".
000066       05 Z34           PIC X(19)  VALUE "TREINTA-Y-CUATRO-  ".
000067       05 Z35           PIC X(19)  VALUE "TREINTA-Y-CINCO-   ".
000068       05 Z36           PIC X(19)  VALUE "TREINTA-Y-SEIS-    ".
000069       05 Z37           PIC X(19)  VALUE "TREINTA-Y-SIETE-   ".
000070       05 Z38           PIC X(19)  VALUE "TREINTA-Y-OCHO-    ".
000071       05 Z39           PIC X(19)  VALUE "TREINTA-Y-NUEVE-   ".
000072       05 Z40           PIC X(19)  VALUE "CUARENTA-          ".
000073       05 Z41           PIC X(19)  VALUE "CUARENTA-Y-UN-     ".
000074       05 Z42           PIC X(19)  VALUE "CUARENTA-Y-DOS-    ".
000075       05 Z43           PIC X(19)  VALUE "CUARENTA-Y-TRES-   ".
000076       05 Z44           PIC X(19)  VALUE "CUARENTA-Y-CUATRO- ".
000077       05 Z45           PIC X(19)  VALUE "CUARENTA-Y-CINCO-  ".
000078       05 Z46           PIC X(19)  VALUE "CUARENTA-Y-SEIS-   ".
000079       05 Z47           PIC X(19)  VALUE "CUARENTA-Y-SIETE-  ".
000080       05 Z48           PIC X(19)  VALUE "CUARENTA-Y-OCHO-   ".
000081       05 Z49           PIC X(19)  VALUE "CUARENTA-Y-NUEVE-  ".
000082       05 Z50           PIC X(19)  VALUE "CINCUENTA-         ".
000083       05 Z51           PIC X(19)  VALUE "CINCUENTA-Y-UN-    ".
000084       05 Z52           PIC X(19)  VALUE "CINCUENTA-Y-DOS-   ".
000085       05 Z53           PIC X(19)  VALUE "CINCUENTA-Y-TRES-  ".
000086       05 Z54           PIC X(19)  VALUE "CINCUENTA-Y-CUATRO-".
000087       05 Z55           PIC X(19)  VALUE "CINCUENTA-Y-CINCO- ".
000088       05 Z56           PIC X(19)  VALUE "CINCUENTA-Y-SEIS-  ".
000089       05 Z57           PIC X(19)  VALUE "CINCUENTA-Y-SIETE- ".
000090       05 Z58           PIC X(19)  VALUE "CINCUENTA-Y-OCHO-  ".
000091       05 Z59           PIC X(19)  VALUE "CINCUENTA-Y-NUEVE- ".
000092       05 Z60           PIC X(19)  VALUE "SESENTA-           ".
000093       05 Z61           PIC X(19)  VALUE "SESENTA-Y-UN-      ".
000094       05 Z62           PIC X(19)  VALUE "SESENTA-Y-DOS-     ".
000095       05 Z63           PIC X(19)  VALUE "SESENTA-Y-TRES-    ".
000096       05 Z64           PIC X(19)  VALUE "SESENTA-Y-CUATRO-  ".
000097       05 Z65           PIC X(19)  VALUE "SESENTA-Y-CINCO-   ".
000098       05 Z66           PIC X(19)  VALUE "SESENTA-Y-SEIS-    ".
000099       05 Z67           PIC X(19)  VALUE "SESENTA-Y-SIETE-   ".
000100       05 Z68           PIC X(19)  VALUE "SESENTA-Y-OCHO-    ".
000101       05 Z69           PIC X(19)  VALUE "SESENTA-Y-NUEVE-   ".
000102       05 Z70           PIC X(19)  VALUE "SETENTA-           ".
000103       05 Z71           PIC X(19)  VALUE "SETENTA-Y-UN-      ".
000104       05 Z72           PIC X(19)  VALUE "SETENTA-Y-DOS-     ".
000105       05 Z73           PIC X(19)  VALUE "SETENTA-Y-TRES-    ".
000106       05 Z74           PIC X(19)  VALUE "SETENTA-Y-CUATRO-  ".
000107       05 Z75           PIC X(19)  VALUE "SETENTA-Y-CINCO-   ".
000108       05 Z76           PIC X(19)  VALUE "SETENTA-Y-SEIS-    ".
000109       05 Z77           PIC X(19)  VALUE "SETENTA-Y-SIETE-   ".
000110       05 Z78           PIC X(19)  VALUE "SETENTA-Y-OCHO-    ".
000111       05 Z79           PIC X(19)  VALUE "SETENTA-Y-NUEVE-   ".
000112       05 Z80           PIC X(19)  VALUE "OCHENTA-           ".
000113       05 Z81           PIC X(19)  VALUE "OCHENTA-Y-UN-      ".
000114       05 Z82           PIC X(19)  VALUE "OCHENTA-Y-DOS-     ".
000115       05 Z83           PIC X(19)  VALUE "OCHENTA-Y-TRES-    ".
000116       05 Z84           PIC X(19)  VALUE "OCHENTA-Y-CUATRO-  ".
000117       05 Z85           PIC X(19)  VALUE "OCHENTA-Y-CINCO-   ".
000118       05 Z86           PIC X(19)  VALUE "OCHENTA-Y-SEIS-    ".
000119       05 Z87           PIC X(19)  VALUE "OCHENTA-Y-SIETE-   ".
000120       05 Z88           PIC X(19)  VALUE "OCHENTA-Y-OCHO-    ".
000121       05 Z89           PIC X(19)  VALUE "OCHENTA-Y-NUEVE-   ".
000122       05 Z90           PIC X(19)  VALUE "NOVENTA-           ".
000123       05 Z91           PIC X(19)  VALUE "NOVENTA-Y-UN-      ".
000124       05 Z92           PIC X(19)  VALUE "NOVENTA-Y-DOS-     ".
000125       05 Z93           PIC X(19)  VALUE "NOVENTA-Y-TRES-    ".
000126       05 Z94           PIC X(19)  VALUE "NOVENTA-Y-CUATRO-  ".
000127       05 Z95           PIC X(19)  VALUE "NOVENTA-Y-CINCO-   ".
000128       05 Z96           PIC X(19)  VALUE "NOVENTA-Y-SEIS-    ".
000129       05 Z97           PIC X(19)  VALUE "NOVENTA-Y-SIETE-   ".
000130       05 Z98           PIC X(19)  VALUE "NOVENTA-Y-OCHO-    ".
000131       05 Z99           PIC X(19)  VALUE "NOVENTA-Y-NUEVE-   ".
000132
000133     03 T-UNIDADES REDEFINES TAB-UNIDADES  PIC X(19) OCCURS 99.
000134
000135 01  ESTRUCTURA-LETRAS.
000136     03 LETRAS           PIC X(179) VALUE SPACES.
000137     03 LETRA REDEFINES LETRAS PIC X OCCURS 179.
000138
000139 01  TER.
000140     03 TEX.
000141        05 NRO-MIL-MILLON   PIC X(19) VALUE SPACES.
000142        05 LETRA-MIL-MILLON PIC X(5)  VALUE SPACES.
000143        05 NRO-CIEN-MILLON  PIC X(19) VALUE SPACES.
000144        05 NRO-MILLONES     PIC X(19) VALUE SPACES.
000145        05 LETRA-MILLONES   PIC X(12) VALUE SPACES.
000146        05 NRO-CIENT-MILES  PIC X(19) VALUE SPACES.
000147        05 NRO-UNID-MILES   PIC X(19) VALUE SPACES.
000148        05 LETRA-MILES      PIC X(5)  VALUE SPACES.
000149        05 NRO-CIENT-UNID   PIC X(19) VALUE SPACES.
000150        05 NRO-UNID-UNID    PIC X(19) VALUE SPACES.
000151        05 LETRA-CENTAVOS   PIC X(24) VALUE SPACES.
000152     03 CAMPOS-ALFA REDEFINES TEX PIC X OCCURS 179.
000153
000154 01  ESTRUCTURA-NUMEROS.
000155     03 MILES-MILLONES       PIC 99 VALUE ZEROS.
000156     03 TRIPLA-MILLONES.
000157        05 CIENTOS-MILLON    PIC 9  VALUE ZEROS.
000158        05 UNIDADES-MILLON   PIC 99 VALUE ZEROS.
000159     03 TRIPLA-MILES.
000160        05 CIENTOS-MILES     PIC 9  VALUE ZEROS.
000161        05 UNIDADES-MILES    PIC 99 VALUE ZEROS.
000162     03 TRIPLA-UNIDADES.
000163        05 CIENTOS-UNIDADES  PIC 9  VALUE ZEROS.
000164        05 UNIDADES-UNIDADES PIC 99 VALUE ZEROS.
000165        
000166 01  ESTRUCTURA-NUMEROS-X REDEFINES ESTRUCTURA-NUMEROS.
000167     03 B                PIC 99.
000168     03 C                PIC 9.
000169     03 D                PIC 99.
000170     03 E                PIC 9.
000171     03 F                PIC 99.
000172     03 G                PIC 9.
000173     03 H                PIC 99.
000174     
000175 01  FIN-VALOR.
000176     03 ESTRUCTURA-FINAL.
000177        05 PESOS-CON     PIC X(10) VALUE SPACES.
000178        05 CTVS          PIC ZZ.
000179        05 RESTOCTVS     PIC X(10) VALUE SPACES.
000180        05 FILLER        PIC XX    VALUE SPACES.
000181        
000182 01 INDICA-FIN-LETRAS   PIC 9 VALUE ZERO.
000183 88 IND-FIN-LETRAS       VALUE 1.
000184
000185 LINKAGE SECTION.
000186 01  DATOS-MONTO.
000187     03 VR-EN-PESOS.
000188        05 VR-PESOS      PIC 9(11).
000189        05 VR-CTVS       PIC V99.
000190     03 FILLER           PIC X.
000191     03 VR-EN-LETRAS.
000192        05 LINEA-LETRAS  PIC X(155).
000193
000194 PROCEDURE DIVISION USING DATOS-MONTO.
000195
000196 MONTO.
000197     MOVE SPACES TO TER.
000198     MOVE SPACES TO ESTRUCTURA-LETRAS.
000199     COMPUTE CENT-EDIT = VR-CTVS * 100.
000200     IF CENT-EDIT = ZEROS
000201        MOVE 0          TO CTVS
000202        MOVE "PESOS"    TO PESOS-CON
000203        MOVE "-M/CTE"   TO RESTOCTVS
000204     ELSE
000205        MOVE "PESOS-CON-" TO PESOS-CON
000206        MOVE CENT-EDIT    TO CTVS
000207        MOVE "/100-M/CTE" TO RESTOCTVS.
000208        MOVE ZEROS TO L.
000209
000210     MOVE ZEROS TO INDICA-FIN-LETRAS.
000211
000212     IF VR-PESOS > ZEROS
000213        MOVE VR-PESOS TO ESTRUCTURA-NUMEROS
000214        PERFORM PROCESO
000215     ELSE
000216        MOVE SPACES TO PESOS-CON
000217        PERFORM COMPLETAR-ESTRUCTURAS VARYING K FROM 1 BY 1
000218                UNTIL K > 155 OR IND-FIN-LETRAS.
000219
000220     INSPECT LETRAS REPLACING ALL " " BY "*"
000221     INSPECT LETRAS REPLACING ALL "-" BY " "
000222     MOVE LETRAS TO LINEA-LETRAS.
000223
000224 FIN-MONTO.
000225     EXIT PROGRAM.
000226
000227 PROCESO.
000228     IF MILES-MILLONES > ZEROS
000229        PERFORM TRATAMIENTO-MILLARES.
000230
000231     IF TRIPLA-MILLONES > ZEROS
000232        PERFORM TRATAMIENTO-MILLONES.
000233
000234     IF TRIPLA-MILES > ZEROS
000235        PERFORM TRATAMIENTO-MILES.
000236
000237     IF TRIPLA-UNIDADES > ZEROS
000238        PERFORM TRATAMIENTO-UNIDADES.
000239
000240     MOVE FIN-VALOR TO LETRA-CENTAVOS.
000241
000242     MOVE ZEROS TO K.
000243     PERFORM TRASLADAR-ESTRUCTURAS
000244             VARYING J FROM 1 BY 1 UNTIL J > 179.
000245
000246     COMPUTE Z = K + 1
000247     PERFORM COMPLETAR-ESTRUCTURAS VARYING K FROM Z BY 1
000248             UNTIL K > 179 OR IND-FIN-LETRAS.
000249
000250 TRATAMIENTO-MILLARES.
000251     MOVE "MIL-" TO LETRA-MIL-MILLON.
000252     MOVE T-UNIDADES (B) TO NRO-MIL-MILLON.
000253
000254     IF  TRIPLA-MILLONES = ZEROS
000255         MOVE "MILLONES-"      TO LETRA-MILLONES.
000256
000257     IF  TRIPLA-MILLONES = ZEROS
000258     AND TRIPLA-MILES    = ZEROS
000259     AND TRIPLA-UNIDADES = ZEROS
000260         MOVE "MILLONES-DE-"   TO LETRA-MILLONES.
000261
000262 TRATAMIENTO-MILLONES.
000263     IF TRIPLA-MILLONES = 100
000264        MOVE CIEN TO NRO-CIEN-MILLON
000265     ELSE
000266        IF CIENTOS-MILLON > 0
000267           MOVE T-CIENTOS (C) TO NRO-CIEN-MILLON
000268        END-IF
000269        IF UNIDADES-MILLON > ZEROS
000270           MOVE T-UNIDADES (D) TO NRO-MILLONES.
000271
000272     IF TRIPLA-MILLONES > 001
000273        MOVE "MILLONES-"    TO LETRA-MILLONES
000274        ELSE MOVE "MILLON-"      TO LETRA-MILLONES.
000275
000276     IF  TRIPLA-MILLONES > 001
000277     AND TRIPLA-MILES = ZEROS
000278     AND TRIPLA-UNIDADES = ZEROS
000279         MOVE "MILLONES-DE-" TO LETRA-MILLONES.
000280
000281     IF  TRIPLA-MILLONES = 001
000282     AND TRIPLA-MILES    = ZEROS
000283     AND TRIPLA-UNIDADES = ZEROS
000284         MOVE "MILLON-DE-"   TO LETRA-MILLONES.
000285
000286 TRATAMIENTO-MILES.
000287     MOVE "MIL-" TO LETRA-MILES.
000288
000289     IF CIENTOS-MILES > ZEROS
000290        MOVE T-CIENTOS (E) TO NRO-CIENT-MILES.
000291
000292     IF  CIENTOS-MILES = 1
000293     AND UNIDADES-MILES = ZEROS
000294         MOVE CIEN TO NRO-CIENT-MILES.
000295
000296     IF UNIDADES-MILES > ZEROS
000297        MOVE T-UNIDADES (F) TO NRO-UNID-MILES.
000298
000299 TRATAMIENTO-UNIDADES.
000300     IF CIENTOS-UNIDADES > 0
000301        MOVE T-CIENTOS (G) TO NRO-CIENT-UNID.
000302
000303     IF CIENTOS-UNIDADES = 1
000304     AND UNIDADES-UNIDADES = ZEROS
000305         MOVE CIEN TO NRO-CIENT-UNID.
000306
000307     IF UNIDADES-UNIDADES > ZEROS
000308        MOVE T-UNIDADES (H) TO NRO-UNID-UNID.
000309
000310     IF TRIPLA-UNIDADES = 001
000311        INSPECT PESOS-CON REPLACING ALL "PESOS-CON-"
000312                                    BY "PESO-CON- "
000313        INSPECT PESOS-CON REPLACING ALL "PESOS" BY "PESO ".
000314
000315 TRASLADAR-ESTRUCTURAS.
000316     IF CAMPOS-ALFA (J) NOT = SPACES
000317        ADD 1 TO K
000318        MOVE CAMPOS-ALFA (J) TO LETRA (K).
000319
000320 COMPLETAR-ESTRUCTURAS.
000321     MOVE 1 TO INDICA-FIN-LETRAS.
000322
